/**
 * Memory of 512 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */
CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
        // 000000000 - 111111111 (0 - 511)
        DMux8Way(
            in=load,
            sel=address[6..8],
            a=loadA,
            b=loadB,
            c=loadC,
            d=loadD,
            e=loadE,
            f=loadF,
            g=loadG, 
            h=loadH
        );

        RAM64(in=in, load=loadA, address=address[0..5], out=outRAM64A);
        RAM64(in=in, load=loadB, address=address[0..5], out=outRAM64B);
        RAM64(in=in, load=loadC, address=address[0..5], out=outRAM64C);
        RAM64(in=in, load=loadD, address=address[0..5], out=outRAM64D);
        RAM64(in=in, load=loadE, address=address[0..5], out=outRAM64E);
        RAM64(in=in, load=loadF, address=address[0..5], out=outRAM64F);
        RAM64(in=in, load=loadG, address=address[0..5], out=outRAM64G);
        RAM64(in=in, load=loadH, address=address[0..5], out=outRAM64H);

        Mux8Way16(
            a=outRAM64A,
            b=outRAM64B,
            c=outRAM64C,
            d=outRAM64D,
            e=outRAM64E,
            f=outRAM64F,
            g=outRAM64G,
            h=outRAM64H,
            sel=address[6..8],
            out=out
        );
}